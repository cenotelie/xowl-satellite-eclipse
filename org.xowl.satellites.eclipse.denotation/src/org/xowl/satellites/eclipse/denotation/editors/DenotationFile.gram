/*******************************************************************************
 * Copyright (c) 2017 Association Cénotélie (cenotelie.fr)
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General
 * Public License along with this program.
 * If not, see <http://www.gnu.org/licenses/>.
 ******************************************************************************/

grammar DenotationFile
{
    options
    {
        Axiom = "denotation";
        Separator = "BLANK";
    }
    terminals
    {
        // separator characters
        fragment NEW_LINE       -> U+000D /* CR */
                                |  U+000A /* LF */
                                |  U+000D U+000A /* CR LF */
                                |  U+2028 /* LS */
                                |  U+2029 /* PS */ ;
        fragment WHITE_SPACE    -> U+0020 | U+0009 | U+000B | U+000C ;
        COMMENT                 -> '#' (.* - (.* NEW_LINE .*)) NEW_LINE ;
        BLANK                   -> NEW_LINE | WHITE_SPACE ;

        IRIREF                  -> '<' (
                                    . - (U+0000 .. U+0020 | [<>"{}|^`\\])
                                    | '\\u' [a-fA-F0-9]{4}
                                    | '\\U' [a-fA-F0-9]{8}
                                )* '>' ;
        fragment PLX            -> '%' [a-fA-F0-9] [a-fA-F0-9]
                                |  '\\' ( [_~.!$&'()*+,;=/?#@%] | '-') ;
        fragment PN_CHARS_BASE  -> [A-Za-z] | U+00C0 .. U+00D6 | U+00D8 .. U+00F6 | U+00F8 .. U+02FF | U+0370 .. U+037D
                                            | U+037F .. U+1FFF | U+200C .. U+200D | U+2070 .. U+218F | U+2C00 .. U+2FEF
                                            | U+3001 .. U+D7FF | U+F900 .. U+FDCF | U+FDF0 .. U+FFFD
                                            | U+10000 .. U+EFFFF ;
        fragment PN_CHARS_U     -> PN_CHARS_BASE | '_' ;
        fragment PN_CHARS       -> PN_CHARS_U | '-' | [0-9] | U+00B7 | U+0300 .. U+036F | U+203F .. U+2040;
        fragment PN_PREFIX      -> PN_CHARS_BASE ((PN_CHARS | '.')* PN_CHARS)? ;
        fragment PN_LOCAL       -> (PN_CHARS_U | ':' | [0-9] | PLX) ((PN_CHARS | '.' | ':' | PLX)* (PN_CHARS | ':' | PLX))? ;
        PNAME_NS                -> PN_PREFIX? ':' ;
        PNAME_LN                -> PNAME_NS PN_LOCAL ;

        // Basic literals
        INTEGER                 -> [0-9]+ ;
        DECIMAL                 -> [0-9]* '.' [0-9]+ ;
        DOUBLE                  -> ( [0-9]+ '.' [0-9]* [eE] ('+' | '-')? [0-9]+
                                                | '.' [0-9]+ [eE] ('+' | '-')? [0-9]+
                                                | [0-9]+ [eE] ('+' | '-')? [0-9]+
                                                ) ;
        fragment STRING_DOUBLE_QUOTE    -> '"' (
                                        . - (U+0022 | U+005C | U+000A | U+000D)
                                        | '\\' ([tbnrf"'] | '\\')
                                        | '\\u' [a-fA-F0-9]{4}
                                        | '\\U' [a-fA-F0-9]{8}
                                        )* '"' ;
        fragment STRING_SINGLE_QUOTE    -> '\'' (
                                        . - (U+0027 | U+005C | U+000A | U+000D)
                                        | '\\' ([tbnrf"'] | '\\')
                                        | '\\u' [a-fA-F0-9]{4}
                                        | '\\U' [a-fA-F0-9]{8}
                                        )* '\'' ;
        STRING                          -> STRING_DOUBLE_QUOTE | STRING_SINGLE_QUOTE ;

        OP_EQ           -> '=';
        OP_NEQ          -> '!=';
        OP_MEMBER       -> '.';

        VARIABLE        -> '?' [a-zA-Z_] [a-zA-Z_0-9]* ;

        BASE            -> ~'base';
        PREFIX          -> ~'prefix';
        TRUE            -> ~'true';
        FALSE           -> ~'false';
        RULE            -> ~'rule';
        IS              -> ~'is';
        SIGN            -> ~'sign';
        SEME            -> ~'seme';
        AND             -> ~'and';
        WITH            -> ~'with';
        RELATION        -> ~'relation';
        BOUND           -> ~'bound';
        BIND            -> ~'bind';
        TO              -> ~'to';
        A               -> ~'a';
        ID              -> ~'id';
        AS              -> ~'as';

        LP              -> '(';
        RP              -> ')';
        LCB             -> '{';
        RCB             -> '}';
        LB              -> '[';
        RB              -> ']';
        COMMA           -> ',';
        COLON           -> ':';
    }
    rules
    {
        denotation              -> prologue denotation_rules ;
        prologue                -> (decl_base | decl_prefix)* ;
        decl_base               -> BASE! IRIREF ;
        decl_prefix             -> PREFIX! PNAME_NS IRIREF ;

        denotation_rules        -> rule* ;
        rule                    -> RULE! STRING signs IS! semes ;

        signs                   -> sign_static^ | sign_patterns^ ;
        sign_static             -> SIGN! iri ;
        sign_patterns           -> sign_pattern (AND! sign_pattern)* ;
        sign_pattern            -> SIGN! VARIABLE sign_pattern_properties sign_pattern_relations sign_pattern_binding ;
        sign_pattern_properties -> sign_pattern_property* ;
        sign_pattern_property   -> WITH! iri (OP_EQ | OP_NEQ) sign_property_value ;
        sign_pattern_relations  -> sign_pattern_relation* ;
        sign_pattern_relation   -> RELATION! iri (VARIABLE | iri) ;
        sign_pattern_binding    -> (BOUND! TO! SEME! (VARIABLE | iri))? ;
        sign_property_value     -> iri^
                                |  literal^
                                |  json_object^ ;

        semes                   -> seme (AND! seme)* ;
        seme                    -> seme_static^
                                |  seme_matched^
                                |  seme_template^ ;
        seme_static             -> SEME!    iri            seme_properties seme_bindings ;
        seme_matched            -> SEME!    VARIABLE       seme_properties seme_bindings ;
        seme_template           -> A! SEME! iri seme_id seme_alias seme_properties seme_bindings ;
        seme_id                 -> ( ID! LP! seme_id_part* RP! )? ;
        seme_id_part            -> iri^
                                |  STRING^
                                |  VARIABLE OP_MEMBER^ iri; // expecting the identifier of a sign as variable
        seme_alias              -> ( AS! VARIABLE )? ;
        seme_properties         -> seme_property* ;
        seme_property           -> WITH! iri OP_EQ! seme_property_value ;
        seme_property_value     -> iri^
                                |  literal^
                                |  VARIABLE^ // expecting alias of a seme
                                |  VARIABLE OP_MEMBER^ iri; // expecting the identifier of a sign as variable
        seme_bindings           -> seme_binding* ;
        seme_binding            -> BIND! TO! SIGN! seme_binding_target^ ;
        seme_binding_target     -> VARIABLE^ | iri^ ;

        iri                     -> IRIREF^ | prefixedName^ ;
        prefixedName            -> PNAME_LN^ | PNAME_NS^ ;
        literal                 -> TRUE^ | FALSE^ | INTEGER^ | DECIMAL^ | DOUBLE^ | STRING^ ;
        json_object             -> LCB! (json_object_member (COMMA! json_object_member)*)? RCB! ;
        json_object_member      -> STRING COLON! json_value ;
        json_array              -> LB! (json_value (COMMA! json_value)*)? RB! ;
        json_value              -> json_object^ | json_array^
                                |  TRUE^ | FALSE^ | INTEGER^ | DECIMAL^ | DOUBLE^ | STRING^ ;
    }
}

